plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

allprojects {
    apply plugin: 'java'

    group = 'fr.milekat'
    version = '1.2.1-SNAPSHOT'

    repositories {
        mavenCentral()
        maven { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
        maven { url = "https://libraries.minecraft.net/" }
        maven { url = "https://maven.pkg.github.com/tutur1004/MilekatUtils" }
    }

    dependencies {
        //  Annotation
        compileOnly group: 'org.jetbrains', name: 'annotations', version: '24.0.1'

        //  Minecraft
        compileOnly group: 'org.spigotmc', name: 'spigot-api', version: '1.20.4-R0.1-SNAPSHOT'
    }

    processResources {
        filesMatching('plugin.yml') {
            expand 'pluginVersion': project.version
        }
    }

    ext {
        githubOwner = 'tutur1004'
        repository = 'MileBanks'
    }
}

jar {
    actions = [] as List<Action<? super Task>>
    dependsOn('shadowJar')
}

shadowJar {
    getArchiveClassifier().set(null)
}

dependencies {
    implementation project('banks-api')

    //  Utils
    implementation(group: 'fr.milekat', name: 'milekat-utils', version: '1.0.3') {
        exclude(group: '*')
    }
    compileOnly group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.15.2'
    compileOnly group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.15.2'
    compileOnly group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: '2.15.2'

    //  Storage clients
    compileOnly group: 'co.elastic.clients', name: 'elasticsearch-java', version: '8.12.0'
}